plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.60'
    id 'maven'
}

group 'com.r3.corda'
version '1.0-SNAPSHOT'

// You can define this property in ~/.gradle/gradle.properties on your dev box.
if (! project.hasProperty('cordaArtifactoryUsername') || ext.cordaArtifactoryUsername == null) {
    ext.cordaArtifactoryUsername = System.getenv('CORDA_ARTIFACTORY_USERNAME')
            ?: System.getProperty('corda.artifactory.username')
}

// You can define this property in ~/.gradle/gradle.properties on your dev box.
if (! project.hasProperty('cordaArtifactoryPassword') || ext.cordaArtifactoryPassword == null) {
    ext.cordaArtifactoryPassword = System.getenv('CORDA_ARTIFACTORY_PASSWORD')
            ?: System.getProperty('corda.artifactory.password')
}

def artifactory_user = ext.cordaArtifactoryUsername
def artifactory_password = ext.cordaArtifactoryPassword

repositories {
    ext.artifactory_contextUrl = 'https://software.r3.com/artifactory'

    if ( artifactory_user != null && artifactory_password != null ){
        maven{ 
            url "$artifactory_contextUrl/corda-enterprise"
            credentials {
                username artifactory_user
                password artifactory_password
            }
        }
//        maven {
//            url "$artifactory_contextUrl/r3-corda-releases"
//            credentials {
//                username artifactory_user
//                password artifactory_password
//            }
//        }
    }
        
    mavenLocal()
    mavenCentral()
    maven { url "$artifactory_contextUrl/corda-dependencies" }
    maven { url "$artifactory_contextUrl/corda" }
    maven { url "$artifactory_contextUrl/r3-corda-releases" }
}

dependencies {

    ext.jmeterVersion = 3.3
    ext.cordaVersion = "4.0"
    ext.cordaOsVersion = "4.0"

    // Kotlin dependency for the Kotlin example
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    // The jmeter-corda package has the base classes that form the glue between JMeter and Corda. All Corda performance
    // samplers need to depend on thi
    compile group: "com.r3.corda", name: "jmeter-corda", version: cordaVersion

    // Any sampler needs to depend on Corda RPC to invoke flows - the RPC client is used to actually drive the node
    compile group: "com.r3.corda", name: "corda-rpc", version: cordaVersion

    // The example sampler use the performance test flows that come with the Corda Performance test suite, therefore
    // it needs to depend on this package so the types are available to create the flows and parse the results.
    // To create a sampler for a different CorDapp, a dependency on that app needs to be added here.
    compile group: "com.r3.corda", name: "corda-ptflows", version: cordaVersion

    // A second sample uses the finance CorDapp to show testing using a difference CorDapp that is not included in the
    // performance suite bundle
    compile group: "net.corda", name: "corda-finance-contracts", version: cordaOsVersion
    compile group: "com.r3.corda", name: "corda-finance-workflows", version: cordaVersion

    // Dependency on Apache JMeter
    compile group: 'org.apache.jmeter', name: 'ApacheJMeter_java', version: jmeterVersion // 'compile' because we extend Java sampler.

}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
